[DefName GlobalPlayerControls]
PreAoSDamage			0	// 1 is on 0 is off.


[Events eGlobalPlayerBehavior]
On=@DClick
CheckExpansion

On=@EquipTest
CheckExpansion

On=@Hit
If (<dDef.PreAoSDamage>)
 If (<Src.FindLayer.LayerHand1.IsWeapon>)
  Ref1=<Src.FindLayer.LayerHand1.UID>
  ArgN1=<R<EVal <Ref1.Dam.Min>/2>,<EVal <Ref1.Dam.Max>*2>>
 Else
  ArgN1=<R1,8>
 EndIf
 Return 0
EndIf

On=@Mount
// GM's Can mount anything; unless the GM is a gargoyle.
If (<Src.IsGm>) && !(<Src.IsGargoyle>)
 Return 0
EndIf

CheckExpansion	// Lets be sure we have the expansion first.

// Gargoyles Cannot mount creatures.
If <Src.IsGargoyle>
 Return 1
EndIf

If (<CanUse> &CanUNone)
 Return 1
ElIf (<Src.IsHuman>) && !(<CanUse> &CanUHuman)
 Return 1
ElIf (<Src.IsElf>) && !(<CanUse> &CanUElf)
 Return 1
ElIf (<Src.IsMale>) && !(<CanUse> &CanUMale)
 Return 1
ElIf (<Src.IsFemale>) && !(<CanUse> &CanUFemale)
 Return 1
EndIf


On=@ItemPickUp_Ground
If (<Act.Amount> == 1) && (<Act.Weight> == 255)
 Return 1
EndIf


On=@ItemPickUp_Self
If (<ArgO.Amount> == 1) && (<ArgO.Weight> == 255)
 Return 1
EndIf


On=@ItemPickUp_Pack
If (<Act.Amount> == 1) && (<Act.Weight> == 255)
 Return 1
EndIf


[EOF]