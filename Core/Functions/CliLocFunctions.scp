[Function ArmorCliLocs]
NameCliLoc
AttrCliLoc
WeightCliLoc
ResistanceCliLoc
ReqStrCliLoc
DurabilityCliLoc


[Function WeaponCliLocs]
NameCliLoc
AttrCliLoc
WeightCliLoc
DamageCliLoc
SpeedCliLoc
ReqStrCliLoc
TwoHandsCliLoc
SkillCliLoc
DurabilityCliLoc


[Function TalismanCliLocs]
NameCliLoc
AttrCliLoc
WeightCliLoc


[Function NameCliLoc]
If (<Can> &CanIStack)
 If (<NameLoc>)
  Src.AddCliLoc 1050039,<Amount>,<NameLoc>
 ElIf (<Name>)
  Src.AddCliLoc 1050039,<Amount>,<Name>
 Else
  If (<DispIDDec> <= 16381)
   Src.AddCliLoc 1050039,<Amount>,<Eval (1020000 + <DispIDDec>)>
  Else
   Src.AddCliLoc 1050039,<Amount>,<Eval (1078872 + <DispIDDec>)>
  EndIf
 EndIf
Else
 If (<NameLoc>)
  Src.AddCliLoc <NameLoc>
 ElIf (<Name>)
  Src.AddCliLoc 1042971,<Name>
 Else
  If (<DispIDDec> <= 16381)
   Src.AddCliLoc <Eval (1020000 + <DispIDDec>)>
  Else
   Src.AddCliLoc <Eval (1078872 + <DispIDDec>)>
  EndIf
 EndIf
EndIf


[Function ArtifactCliLoc]
If (<Attr> &AttrArtifact)
 Src.AddCliLoc 1061078,<Rarity>
EndIf


// Since insured, blessed, newbie, or insured can be in together, we will process them through one function.
[Function AttrCliLoc]
If (<Attr> &AttrNewbie|AttrBlessed)
 Src.AddCliLoc 1038021		// Blessed
ElIf (<Attr> &AttrNewbie)
 Src.AddCliLoc 1042971,Newbie
ElIf (<Attr> &AttrCursed)
 Src.AddCliLoc 1049643	// cursed
ElIf (<Attr> &AttrInsured)
 Src.AddCliLoc 1114311	// Insured
EndIf

If (<CraftedBy>)
 Src.AddCliLoc 1050043,<CraftedBy>	// Crafted by ~1_Name~
EndIf

If (<Attr> &AttrExceptional)
 Src.AddCliLoc 1018303	// Exceptional
EndIf

If (<Attr> &AttrStolen)
 Src.AddCliLoc 1072948	// Stolen
EndIf

If (<Attr> &AttrAccountBound)
 Src.AddCliLoc 1155526	// Account Bound
EndIf

If (<Attr> &AttrShardBound)
 Src.AddCliLoc 1155536	// Shard Bound
EndIf

[Function CanUseCliLoc]
If (<CanUse> &CanUGargoyle) && !(<CanUSe> &CanUElf|CanUHuman)
 Src.AddCliLoc 1111709	// Gargoyles Only
ElIf (<CanUse> &CanUElf) && !(<CanUSe> &CanUGargoyle|CanUHuman)
 Src.AddCliLoc 1075086	// Elves Only
EndIf


[Function WeightCliLoc]
If (<Amount> > 1) && (<BaseWeight> != 255) && (<BaseWeight>)
 Src.AddCliLoc <QVal (<BaseWeight> > 1) ? 1072789 : 1072788>,<FVal <BaseWeight>>
ElIf (<Amount> > 1) && (<Weight> != 255) && (<Weight>)
 Src.AddCliLoc <QVal (<Weight> > 1) ? 1072789 : 1072788>,<FVal <Weight>>
EndIf


[Function ResistanceCliLoc]
If (<ResPhysical>)
 Src.AddCliLoc 1060448,<ResPhysical>
EndIf
If (<ResFire>)
 Src.AddCliLoc 1060447,<ResFire>
EndIf
If (<ResCold>)
 Src.AddCliLoc 1060445,<ResCold>
EndIf
If (<ResPoison>)
 Src.AddCliLoc 1060449,<ResPoison>
EndIf
If (<ResEnergy>)
 Src.AddCliLoc 1060446,<ResEnergy>
EndIf


[Function ReqStrCliLoc]
If (<ReqStr>)
 Src.AddCliLoc 1061170,<ReqStr>
EndIf


[Function DamageCliLoc]
If (<DamPhysical>)
 Src.AddCliLoc 1060403,<DamPhysical>
EndIf
If (<DamFire>)
 Src.AddCliLoc 1060405,<DamFire>
EndIf
If (<DamCold>)
 Src.AddCliLoc 1060404,<DamCold>
EndIf
If (<DamPoison>)
 Src.AddCliLoc 1060406,<DamPoison>
EndIf
If (<DamEnergy>)
 Src.AddCliLoc 1060407,<DamEnergy>
EndIf
If (<DamChaos>)
 Src.AddCliLoc 1072846,<DamChaos>
EndIf
Src.AddCliLoc 1061168,<Dam.Lo>,<Dam.Hi>


[Function SpeedCliLoc]
Src.AddCliLoc 1061167,<QVal (<Tag0.Override.Speed>) ? <Tag.Override.Speed> : <Speed>>


[Function TwoHandsCliLoc]
Src.AddCliLoc <QVal <TwoHands> ? 1061171 : 1061824>


[Function SkillCliLoc]
If (<Skill> == Swordsmanship)
 Src.AddCliLoc 1061172
ElIf (<Skill> == MaceFighting)
 Src.AddCliLoc 1061173
ElIf (<Skill> == Fencing)
 Src.AddCliLoc 1061174
ElIf (<Skill> == Archery)
 Src.AddCliLoc 1061175
ElIf (<Skill> == Throwing)
 Src.AddCliLoc 1112075
EndIf


[Function DurabilityCliLoc]
If (<HitPoints>)
 Src.AddCliLoc 1060639,<More1L>,<More1H>
EndIf


[Function AmmoCliLoc]
If (<FindType.tArrow>)
 Src.AddCliLoc 1075265,<Count>,<Tag.MaxAmmo>
ElIf (<FindType.tCrossbowBolt>)
 Src.AddCliLoc 1075266,<Count>,<Tag.MaxAmmo>
EndIf


[Function ContCliLoc]
Src.AddCliLoc 1072241,<Count>,<Tag0.Override.MaxItems>,<ContentWeight>,<Tag0.Override.MaxWeight>	// Contents: ~1_COUNT~/~2_MAXCOUNT~ items, ~3_WEIGHT~/~4_MAXWEIGHT~ stones


[EOF]