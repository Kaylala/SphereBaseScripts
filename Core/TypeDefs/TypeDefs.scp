[TypeDef tChickenCoop]


[TypeDef tLitLight]
On=@ClientTooltip
NameCliLoc
AttrCliLoc
WeightCliLoc
If (<SpellChanneling>)
 Src.AddCliLoc 1060482	// Spell Channeling
EndIf
If (<FasterCasting>)
 Src.AddCliLoc 1060413,<FasterCasting>
EndIf
Return 1


[TypeDef tUnlitLight]
On=@ClientTooltip
NameCliLoc
AttrCliLoc
WeightCliLoc
If (<SpellChanneling>)
 Src.AddCliLoc 1060482	// Spell Channeling
EndIf
If (<FasterCasting>)
 Src.AddCliLoc 1060413,<FasterCasting>
EndIf
Return 1


[Comment tSendingContainer]
1054104;a bag of sending

Old CliLocs (Not used now)
1054106;This item will not function while being traded.
1054108;The bag of sending rejects the cursed item.
1054109;The bag of sending rejects that item.
1054151; was placed in your bank box.
1079428;You cannot send a container through the bag of sending

// Selectors for Newer CliLocs
1150423;bag of sending
1150424;chest of sending


[TypeDef tSendingContainer]
On=@ClientTooltip
NameCliLoc
AttrCliLoc
WeightCliLoc
Src.AddCliLoc 1054132,<UsesCur>	// [charges: ~1_charges~]
If (<BaseID> == iChestOfSending)
 Src.AddCliLoc 1150598	// Auto Recharge
EndIf
Return 1

On=@ContextMenuRequest
If (<BaseID> == iBagOfSending)
 If (<UsesCur>)
  Src.AddContextEntry 200,3006189	// Use the Bag of Sending
 Else
  Src.AddContextEntry 200,3006189,01
 EndIf
Else
 If (<UsesCur>)
  Src.AddContextEntry 200,1150419	// Use the Chest of Sending
 Else
  Src.AddContextEntry 200,1150419,01
 EndIf
EndIf

On=@ContextMenuSelect
If (<UsesCur> == 0)
 Return 1
EndIf

If (<TopObj> != <Src>)
 Src.SMsgL -1,1054107	// This item must be in your backpack.
 Return 1
EndIf

If (<Cont.Type> == tEqTradeWindow)
 Src.SMsgL -1,1150422	// The ~1_NAME~ will not function while being traded.
 Return 1
EndIf

Target
Return 1

On=@TargOn_Item
Ref1=<Src.FindLayer.LayerBankBox.UID>

If (<ArgO.TopObj> != <Src>)
 Src.SMsgL -1,1054152	// You may only send items from your backpack to your bank box.
 Return 1
EndIf

If (<Ref1.FCount> == <Ref1.Tag.Override.MaxItems>)
 Src.SMsgL -1,1054110	// Your bank box is full.
 Return 1
EndIf

// 1150421;The ~1_NAME~ rejects that item.
If (<ArgO.Attr> &AttrCursed) || (<ArgO.Attr> &AttrQuestItem) || (<ArgO.Attr> &AttrStolen)
 If (<BaseID> == iBagOfSending)
  Src.SMsgL -1,1150421,#1150423
  Return 1
 Else
  Src.SMsgL -1,1150421,#1150424
  Return 1
 EndIf
EndIf

// 1150420;You cannot send a container through the ~1_NAME~.
If (<ArgO.Type> == tContainer)
 If (<BaseID> == iBagOfSending)
  Src.SMsgL -1,1150420,#1150423
 Else
  Src.SMsgL -1,1150420,#1150424
 EndIf
EndIf

Src.SMsgL -1,1054150;The item was placed in your bank box.
UsesCur --
ArgO.Cont=<Ref1>
//If (<BaseID> == iChestOfSending)
// Recharge
//EndIf
Return 1


[Comment Mailbox]
This could be expanded more to actually do mailing. As is, it is more like a spare container for yourself
or your housing. When used in the housing it can only have items placed in it depending on your privacy.
Items gets temporary CliLocs (Set with a type and timerf for removal?) that are removed. Allegedly,
there is a message that gets attached, but without being on EA, not entirely sure what is used.

Temporary CliLoc Added to Items:
1113938;Sender: ~1_NAME~

Option to Ban sender (Adds to house banned list)
1113939;Ban Sender

Standard System CliLocs
1113941;You can only receive mail if your box is secured, not locked down!
1113942;You may only do that from inside your house.


[TypeDef tMailbox]
On=@DropOn_Self
If (<FCount> == 150)
 Src.SMsgL -1,1113940	// That mailbox is completely full.
 Return 1
EndIf

If (<DispIDDec> == 04140)
 DispID=04141
ElIf (<DispIDDec> == 04142)
 DispID=04143
EndIf
Update

On=@PickUp_Self
If !(<FCount>)
 If (<DispIDDec> == 04141)
  DispID=04140
 ElIf (<DispIDDec> == 04143)
  DispID=04142
 EndIf
 Update
EndIf
	
	
[TypeDef tPilot]
On=@DClick
ref1=<region.uid>
if ( <ref1.more1> == <src> )
 if ( <ref1.pilot> == <src> )
  src.consume 1 i_memory_ship_pilot
  ref1.pilot=0
 else
  ref1.pilot=<src>
  if (<src.FindLayer.layer_horse>)
   src.Dismount
  endif
  serv.newitem = i_memory_ship_pilot
  new.equip
 endif
endif
return 1


[ItemDef iShipPilotMemory]
ID=03e96	//This item is like a horse for the client, but for ships, having it equiped = pilot mode on current ship.
Type=tEqMount
Layer=25

Category=Ship Parts
Subsection=Pilot
Description=Pilot memory


[TypeDef tNormal]
On=@ClientTooltip
NameCliLoc
AttrCliLoc
WeightCliLoc
Return 1


[TypeDef tIngot]
On=@ClientTooltip
NameCliLoc
AttrCliLoc
WeightCliLoc
Return 1


[TypeDef tOre]
On=@ClientTooltip
NameCliLoc
AttrCliLoc
WeightCliLoc
Return 1


// Still need to add in 500 check.
[TypeDef tQuiver]
On=@Create
Tag.MaxAmmo=500
Tag.Override.MaxItems=1
Tag.Override.MaxWeight=50

On=@DropOn_Self
If (<ArgO.Type> != tArrow) || (<ArgO.Type> != tCrossbowBolt)
 Return 1
EndIf


[TypeDef tClockworkAssembly]
On=@DClick
If !(<Src.Tinkering>)
 Src.SMsgL -1,1071940	// You are not quite sure how to use this.
 Return 1
EndIf
If (<Src.Tinkering> < 700)
 Src.SMsgL -1,1071943	// You must be a journeyman or higher tinker to construct a golem.
 Return 1
EndIf
If (<Src.Tag0.GolemDelay>)
 Src.SMsgL -1,1071942	// You are too drained to make another golem. You must wait approximately one day to construct another.
 Return 1
EndIf
If (<TopObj> != <Src.UID>)
 Src.SMsgL -1,1071944	// The clockwork assembly must be in your backpack to construct a golem.
 Return 1
EndIf
If <Src.CurFollower> > <Eval (<MaxFollowers> - 3)>
 Src.SMsgL -1,1049607	// You have too many followers to control that creature.
 Return 1
EndIf
If !(<Src.Restest 1 iPowerCrystal>)
 Src.SMsgL -1,1071945	// You need a power crystal to construct a golem.
 Return 1
ELIf !(<Src.Restest 5 iGears>)
 Src.SMsgL -1,1071946	// You need more gears to construct a golem.
 Return 1
ElIf !(<Src.Restest 50 iBronzeIngot>)
 Src.SMsgL -1,1071947	// You need more bronze ingots to construct a golem.
 Return 1
ElIf !(<Src.Restest 50 iIronIngot>)
 Src.SMsgL -1,1071948	// You need more iron ingots to construct a golem.
 Return 1
EndIf
If (<R1,1000> > <Eval ((<Src.Tinkering> - 700) * 2)>)
 Src.SMsgL -1,1071941	// You failed to make the golem.
 Return 1
Else
 Serv.NewNpc cGolem
 New.Fame=10
 New.Karma=10
 New.OStr=<MulDiv {251 350},<Src.Tinkering>,1000>
 New.ODex=<MulDiv {76 100},<Src.Tinkering>,1000>
 New.OInt=<MulDiv {101 150},<Src.Tinkering>,1000>
 New.MaxHits=<MulDiv {151 210},<Src.Tinkering>,1000>
 New.Wrestling=<MulDiv {601 1000},<Src.Tinkering>,1000>
 New.Tactics=<MulDiv {601 1000},<Src.Tinkering>,1000>
 New.MagisResistance=<MulDiv {1501 1900},<Src.Tinkering>,1000>
 New.Parrying=<MulDiv {601 1000},<Src.Tinkering>,1000>
 New.ResPhysical={35 55}
 New.ResFire={50 60}
 New.ResCold={10 30}
 New.ResPoison={99 100}
 New.ResEnergy={30 40}
 New.P=<Src.P>
 New.MakeMyPet
 Src.CurFollowers += <New.ControlSlots>
 Src.Tag.GolemDelay=1
 Src.TimerF 864000,GolemDelay	// 24 hours; inclusive of the deciseconds multiplier.
 Src.Sound 0241
 Update
EndIf

[Function GolemDelay]
Src.Tag.GolemDelay=


// Need to check what More is doing in this.
// Moving what the hair grows into to TData4.
[TypeDef tHair]
On=@Create
Attr |= AttrNewbie
Timer=-1	// Change this if you want hair to grow.
More=1		// What is this doing?

On=@Timer
If !(<Cont>)	// On ground
 Remove <UID>
 Return 1
EndIf

If (<Cont.NPC>)	// Non-players don't have hair growth.
 Return 1
EndIf

Local.Hair=<Cont.FindLayer.LayerHair.UID>
If (<TData4>)
 Serv.NewItem <TData4>,1
 New.More=1
 New.Color=<Cont.FindLayer.LayerHair.Color>
 New.Equip <Cont.UID>
 Remove <Local.Hair>
ElIf !(<TData4>)
 Remove <Local.Hair>
EndIf
Return 1


[TypeDef tBeard]
On=@Create
Attr |= AttrNewbie
Timer=-1	// Change this if you want hair to grow.
More=1		// What is this doing?

On=@Timer
If !(<Cont>)	// On ground
 Remove <UID>
 Return 1
EndIf

If (<Cont.NPC>)	// Non-players don't have hair growth.
 Return 1
EndIf

Local.Hair=<Cont.FindLayer.LayerFacial.UID>
If (<TData4>)
 Serv.NewItem <TData4>,1
 New.More=1
 New.Color=<Cont.FindLayer.LayerFacial.Color>
 New.Equip <Cont.UID>
 Remove <Local.Hair>
ElIf !(<TData4>)
 Remove <Local.Hair>
EndIf
Return 1


// Every 150 Gold Coins is 1 stone. (This used to be every 50)
[TypeDef tGold]
On=DropOn_Item
If (<ArgO.BaseID> == <BaseID>)
Argo.BaseWeight=<Eval ((<Amount> + <ArgO.Amount>) / 150)>
If !(<ArgO.BaseWeight>)
 ArgO.BaseWeight=1
EndIf

On=DropOn_Self
If (<ArgO.BaseID> == <BaseID>)
Argo.BaseWeight=<Eval ((<Amount> + <ArgO.Amount>) / 150)>
If !(<ArgO.BaseWeight>)
 ArgO.BaseWeight=1
EndIf


[TypeDef tCoin]
On=DropOn_Item
If (<ArgO.BaseID> == <BaseID>)
Argo.BaseWeight=<Eval ((<Amount> + <ArgO.Amount>) / 150)>
If !(<ArgO.BaseWeight>)
 ArgO.BaseWeight=1
EndIf

On=DropOn_Self
If (<ArgO.BaseID> == <BaseID>)
Argo.BaseWeight=<Eval ((<Amount> + <ArgO.Amount>) / 150)>
If !(<ArgO.BaseWeight>)
 ArgO.BaseWeight=1
EndIf


[TypeDef tSmokeBomb]
// Needs updating and possible Skill rescript.
On=@DClick
If (<Src.Flags> &StatFHidden) || (<Src.Flags> &StatFInvisible)
 Src.SMsg You are already hidden.
 Return 1
EndIf
Sound=sndSmokeBomb
Src.Action=-1
Src.Action=Skill_Hiding
Src.ActDiff=1
Consume 1 <I>


[EOF]